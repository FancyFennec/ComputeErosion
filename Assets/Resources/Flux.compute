#pragma kernel CSMain
#include <helpers.cginc>

RWTexture2D<float> height;
RWTexture2D<float> water;
RWTexture2D<float4> flux;

uint resolution;
float dTime;
float l;
float A;

float4 computeDeltaH(uint3 id);
float4 computeFlux(uint3 id, float4 dH);
float computeScalingFactor(uint3 id, float4 f);

[numthreads(8, 8, 1)]
void CSMain(uint3 id : SV_DispatchThreadID)
{
    float4 dH = computeDeltaH(id);
    float4 f = computeFlux(id, dH);
    float k = computeScalingFactor(id, f);
    flux[id.xy] = k * f;
}

float4 computeDeltaH(uint3 id) 
{
    float dH[4];
    for (int i = 0; i < 4; i++)
    {
        uint2 index = getIndex(id, i, resolution);
        dH[i] =  water[id.xy].x + height[id.xy] - water[index].x - height[index];
    }
	
    return float4(dH[0], dH[1], dH[2], dH[3]);
}

float4 computeFlux(uint3 id, float4 dH)
{
    float c = A * 9.81 / l; // A * g / l
    return max(0.0, flux[id.xy] + dTime * c * dH);
}

float computeScalingFactor(uint3 id, float4 f)
{
    float vOut = dTime * (f.x + f.y + f.z + f.w);
    if (vOut > 0)
    {
        return min(1.0, l * l * water[id.xy] / vOut);
    }
    else
    {
        return 1.0;
    }
}